GoogleCalendarModel:
  owner=Person
  events*=GoogleCalendarEvent
  taslists*=GoogleTasklist
  tasks*=GoogleCalendarTask
  findEvents*=FindEventsByTimeAndOwner
  findTasks*=ListCompletedTasks
;

GoogleCalendarEvent:
  'Event'  '"' title=Sentance '"' '{'
  'description:' description=Sentance ';'
  'time:' time=GoogleCalendarAppoitment ';'
  ('recurrence:' recurrence=RecurrenceRule ';')?
  ('guests:' guests*=mailAdrRule[',']';')?
  ('status:' status=EventStatus ';')?
  ('visibility:' visibility=EventVisibility ';')?
  ('notifications:' notifications*=EventNotification[','] ';')?
  ('guestsCanSeeOtherGuests:' guestsCanSeeOtherGuests=BOOL';')?
  ('guestsCanInviteOthers:' guestsCanInviteOthers=BOOL ';')?
  '}'
;

GoogleTasklist:
  'Tasklist' '"' title=Sentance '";'
;

GoogleCalendarTask:
  'Task' '"' title=Sentance '"' ':'
  ('"' description=Sentance '"' ',')?
  date=Date ','
  time=Time ';'
;

GoogleCalendarAppoitment:
  '{'
    'startDate: ' eventStartDate=Date ';'
    'endDate: ' eventEndDate=Date ';'
    ('startTime: ' eventStartTime=Time ';')?
    ('endTime: ' eventEndTime=Time ';')?
    ('location: ' eventLocation=Sentance ';')?
    ('timeZone: ' eventTimeZone=TimeZone ';')?
  '}'
;

TimeZone:
  /[A-Za-z_\/+~-]+/
;

RecurrenceRule:
  '{'
    'freq=' freq=FrequentlyRepeatRule ';' 
    ('count=' count=INT ';')?
    ('byDay=' byDay+=RepeatDay[','] ';')? 
    ('byMonth=' byMonth+=INT[','] ';')? 
    ('byMonthDay=' byMonthDay+=INT[','] ';')?
    ('until=' until=Date ';')? 
    ('interval=' interval=INT ';')?
  '}'
;

EventNotification:
    method=EventNotificationMethod 
    remindMeFor+=EventNotificationReminder['-']
;

EventNotificationReminder:
  timeFrame=EventNotificationTimeFrame
  count=INT
;

EventNotificationMethod:
 ('email' | 'popup' | 'sms')
;

EventNotificationTimeFrame:
  ('weeks' | 'days' | 'hours' | 'minutes')
;

FrequentlyRepeatRule:
  ('DAILY' | 'WEEKLY' | 'MONTHLY' | 'YEARLY')
;

RepeatDay:
  ('MO' | 'TU'| 'WE'| 'TH'| 'FR'| 'ST' | 'SU')
;

EndsRule:
  'never' | 'on' Date | 'after' INT 'occurrences'
;

EventStatus:
  'confirmed' | 'tentative' | 'cancelled' 
;

EventVisibility:
  'public' | 'private' | 'default'
;

FindEventsByTimeAndOwner:
  'Find events where'
    'owner is' '"' owner=mailAdrRule '"' 'and'
    'time is between' '"' start=Date '"' 'and' '"' end=Date '"'
;

ListCompletedTasks:
  'List' status=TaskStatus 'tasks in tasklist: ' '"' tasklist=Sentance '"'
;

TaskStatus:
  'completed' | 'pending'
;

Person:
  'person' email=mailAdrRule
;

mailAdrRule:
  /[\w\.]+@([\w]+\.)+[\w]{2,4}/
;

Time:
 hour=INT ':' minute=INT
;

Date:
  year=INT '-' month=INT '-' day=INT 
;

Comment:
    /\/\/.*$/
;

Sentance:
  /[^";]*/
;
